plugins {
  id 'application'
  id 'com.adarshr.test-logger' version '3.2.0'
  id 'org.openjfx.javafxplugin' version '0.0.13'
  id "org.beryx.jlink" version "2.25.0"
//   id "org.javamodularity.moduleplugin" version "1.8.10"
  id "com.github.ben-manes.versions" version "0.42.0"
}

version = "1.0.0"

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "MBARI"
        url = uri("https://maven.pkg.github.com/mbari-org/maven")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
    // maven {url 'http://dl.bintray.com/jerady/maven'}
    // maven {url 'http://dl.bintray.com/hohonuuli/maven' }
    // maven {url 'http://dl.bintray.com/org-mbari/maven' }
}

javafx {
    version = "18.0.1"
    modules = [ 
        'javafx.base',
        'javafx.controls',
        'javafx.fxml',
        'javafx.graphics', 
        'javafx.media', 
        'javafx.swing'
    ]
}

def runtimeJvmArgs = [
        '-Xms1g',
        '--add-exports', 'javafx.base/com.sun.javafx.binding=com.jfoenix',
        '--add-exports', 'javafx.base/com.sun.javafx.event=com.jfoenix',
        '--add-exports', 'javafx.controls/com.sun.javafx.scene.control.behavior=com.jfoenix',
        '--add-exports', 'javafx.controls/com.sun.javafx.scene.control=com.jfoenix',
        '--add-exports', 'javafx.graphics/com.sun.javafx.stage=com.jfoenix',
        '--add-reads', 'jsharktopoda.merged.module=org.slf4j',
]

test {
    moduleOptions {
        runOnClasspath = true
    }
    testlogger {
        theme 'mocha' // task level
    }
}

def vcr4j = "5.0.0.jre17-SNAPSHOT"
dependencies {
    implementation "com.jfoenix:jfoenix:9.0.10"
    implementation "io.reactivex.rxjava3:rxjava:3.1.5"
    implementation "org.kordamp.ikonli:ikonli-javafx:12.3.1"
    implementation "org.kordamp.ikonli:ikonli-material-pack:12.3.1"
    implementation "org.mbari:mbarix4j:2.0.5.jre11"
    implementation "org.mbari.vcr4j:vcr4j-core:${vcr4j}"
    implementation "org.mbari.vcr4j:vcr4j-remote:${vcr4j}"
    implementation "org.slf4j:slf4j-api:2.0.0-alpha7"
    runtimeOnly('ch.qos.logback:logback-classic:1.3.0-alpha16') {
        exclude group: 'javax.activation'
    }
    runtimeOnly 'javax.servlet:javax.servlet-api:4.0.1'
    runtimeOnly 'org.fusesource.jansi:jansi:2.4.0'
    testImplementation 'junit:junit:4.13.2'
}


// mainClassName = 'jsharktopoda/org.mbari.m3.jsharktopoda.javafx.JSharktopoda'
application {
    mainModule = 'jsharktopoda'
    mainClassName = 'org.mbari.m3.jsharktopoda.javafx.JSharktopoda'
    applicationDefaultJvmArgs = runtimeJvmArgs
}



jar {
    manifest {
        attributes 'Main-Class': 'org.mbari.m3.jsharktopoda.javafx.JSharktopoda'
    }
}


jlink {

    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    
    launcher {
        name = 'jsharktopoda'
    }

    addExtraDependencies("javafx")

    jpackage {
        // jpackageHome = System.getenv("JPACKAGE_HOME")
        // jpackageHome = "C:\\Program Files\\AdoptOpenJDK\\jdk-14.0.0.36-hotspot"

        def customInstallerOptions = [
                "--app-version", project.version,
                "--copyright", "Monterey Bay Aquarium Research Institute 2020",
                "--name", project.name,
                "--vendor", "org.mbari"
        ]

        if  (org.gradle.internal.os.OperatingSystem.current().isLinux()) {
            installerType = "deb"
        }
        else if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            installerType = "msi"
            customInstallerOptions.addAll([
                "--win-upgrade-uuid", "bd89f21a-a2c3-49c1-9502-345199c3ed8e",
                "--win-menu-group", "VARS",
                "--win-menu"
            ])
        }
        else {
            installerType = "dmg"
            customInstallerOptions.addAll([
                "--mac-package-name", "jsharktopoda",
                "--mac-package-identifier", project.name
            ])
        }
        installerOptions = customInstallerOptions

    }
}